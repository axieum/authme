plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowCommon(project(path: ':common', configuration: 'transformProductionFabric')) { transitive false }

    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    modImplementation include("com.teamresourceful.resourcefulconfig:resourcefulconfig-fabric-${project.resourceful_config_version}")
    modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"
}

processResources {
    inputs.property 'mod_id', project.mod_id
    inputs.property 'mod_name', project.mod_name
    inputs.property 'mod_description', project.mod_description
    inputs.property 'mod_author', project.mod_author
    inputs.property 'mod_license', project.mod_license
    inputs.property 'version', project.version
    inputs.property 'minecraft_version', project.minecraft_version
    inputs.property 'fabric_loader_version_range', project.fabric_loader_version_range

    // fabric.mod.json
    filesMatching('fabric.mod.json') {
        expand([
            'mod_id': project.mod_id,
            'mod_name': project.mod_name,
            'mod_description': project.mod_description,
            'mod_author': project.mod_author,
            'mod_license': project.mod_license,
            'version': project.version,
            'minecraft_version': project.minecraft_version,
            'fabric_loader_version_range': project.fabric_loader_version_range,
        ])
    }
}

jar {
    archiveAppendix.set project.name
    archiveClassifier.set 'dev'
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveAppendix.set project.name
    archiveClassifier.set 'dev-shadow'
}

remapJar {
    dependsOn tasks.shadowJar
    setInput tasks.shadowJar.archiveFile
    archiveAppendix.set project.name
    archiveClassifier.set null
}

sourcesJar {
    def commonSources = project(':common').tasks.sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
    archiveAppendix.set project.name
}

javadocJar {
    def commonJavadocs = project(':common').tasks.javadocJar
    dependsOn commonJavadocs
    from commonJavadocs.archiveFile.map { zipTree(it) }
    archiveAppendix.set project.name
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
